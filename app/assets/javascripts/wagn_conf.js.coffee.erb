wagnConf = {
  
  tinyMCEConfig : `{ <%= System.setting('*tiny mce') %> }`
  
  editorContentFunctionMap : {
    '.tinymce-textarea'      : -> tinyMCE.getInstanceById(@id).getContent(),
    '.pointer-select'        : -> pointerContent $(this).val(),
    '.pointer-multiselect'   : -> pointerContent $(this).val(),
    '.pointer-radio-list'    : -> pointerContent $(this).find('input:checked').val(),
    '.pointer-list-ul'       : -> pointerContent $(this).find('input'        ).map( -> $(this).val() ),
    '.pointer-checkbox-list' : -> pointerContent $(this).find('input:checked').map( -> $(this).val() ),
    '.perm-editor'           : -> permissionsContent $(this) # must happen after pointer-list-ul, I think
  }
  
  editorInitFunctionMap : {
    '.tinymce-textarea' : -> wagnConf.initTinyMCE()
  }

  initTinyMCE: ()->
    conf = if wagnConf.tinyMCEConfig? then wagnConf.tinyMCEConfig else {}
    conf['mode'] = "specific_textareas"
    conf['editor_selector'] = @id
    tinyMCE.init conf

}

$('.set_select').live 'change', ->
  $(this).closest('form').submit()

$('.pointer-item-add').live 'click', (event)->
  last_item = $(this).closest('ul').find '.pointer-li:last'
  new_item = last_item.clone()
  new_item.find('input').val ''
  last_item.after new_item
  event.preventDefault(); # Prevent link from following its href

$('.pointer-item-delete').live 'click', ->
  item = $(this).closest 'li'
  if item.closest('ul').find('.pointer-li').length > 1
    item.remove()
  else
    item.find('input').val ''
  event.preventDefault(); # Prevent link from following its href

$('.perm-vals input').live 'click', ->
  $(this).slot().find('#inherit').attr('checked',false)
  
$('.perm-editor #inherit').live 'click', ->
  slot = $(this).slot()
  slot.find('.perm-group input:checked').attr('checked', false)
  slot.find('.perm-indiv input').val('')

permissionsContent = (ed) ->
  return '_left' if ed.find('#inherit').attr('checked')
  groups = ed.find('.perm-group input:checked').map( -> $(this).val() )
  indivs = ed.find('.perm-indiv input'        ).map( -> $(this).val() )
  pointerContent $.makeArray(groups).concat($.makeArray(indivs))

pointerContent = (vals) ->
  list = $.map $.makeArray(vals), (v)-> if v then '[[' + v + ']]'
  $.makeArray(list).join "\n"




window.wagnConf = wagnConf